(BOOKMARKS) (SPACE)
CREATE BOOKMARKS
++++++++++
[<++++++++++++
<+++++++++++
<++++++++++
<+++++++++
<++++++++
<+++++++
<++++++
<+++++
<++++
<+++
<+
>>>>>>>>>>>-]
10 30 40 50 60 70 80 90 100 110 120 {SPACE}

<<<<<<+++++++.>>>>+++++++.<++++++++.>-.<---.>----.<+++.>>+. multiply
<<<<<<<<<++.
>>>>>++++.>>>+++++++.--------. two
<<<<<<<<.
>>>>+.>>>>++++++.<+.<++++++++.>--------.>---.+.<<<<<<<<+. numbers
<.
>>>>>>>>>-----.+++++++.<++++++++.<<<<<<<-.>+++++++++.>++++++++.<<. num 1

[>]

10 32 49 58 60 78 84 98 109 117 121 {SPACE}

>,>++++[<------------>-]

<<<<<<<<<<<<<.
>>>>>>>>>-------.+++++++.<.<<<<<<<.>>--------.++++++++.<<. num 2

[>]>>,>++++[<------------>-]

10 32 49 58 60 78 84 98 109 117 121 SPACE val1 val2 {SPACE}

<<[<+>>>+<<-]
>>[<<+>>-]


<<[>[>+>+<<-]>>[<<+>>-]<<<-]

<[>+<-]>>>

BM SPACE val1 val2 {total}

[>+<-] > [<+>>+<-]
total {SPACE} copy

count true false save


>[COUNT
>+

CHECK COUNT FOR 10
----------
[>->+<<[>>>+<<<-]] >+
[<<<+<---------->>>>-]
> [>[<<<+>>>-]<<<++++++++++>>><-]

SAVE
<<<-
]

CLEAN UP AND FORMAT OUTPUT
>[-]
<<<[>>+<<-]

>
[>>->+<<<[>>>>+<<<<-]] >>+
[-]
> [<<<<++++[>++++++++++++<-]>>>>-]

>[<<<<+>>>>-]

<< ++++[<++++++++++++>-]

<<<<[<]<<[>]

++++[>++++++++++++>++++++++++++<<-]

BM {SPACE} mults SPACE digs
10 32 49 58 60 78 84 98 109 117 121

<<<<<<<<<<<.
>[>]
>.
<[<]
<<<<<<<<<<.
>>>>>>>>>-.
<<<<<<<<<.
>[>]
>>.
<[<]
<<<<<<<<<<.
>>>+.
<<<.
>[>]
>[>]
>.>.

FINAL FORM
10 32 49 58 61 78 84 98 109 117 120 SPACE mult1 mult2 SPACE dig1 dig2